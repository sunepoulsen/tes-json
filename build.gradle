import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:7.3.2'
    }
}

plugins {
    id 'org.jetbrains.changelog'
    id 'net.researchgate.release'
    id 'org.sonarqube'
    id 'jacoco'
}

apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'org.owasp.dependencycheck'

group 'dk.sunepoulsen.tes'
version "${project.property('version')}"

repositories {
    mavenLocal()
    mavenCentral()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "${System.properties['maven.repository.base.url']}/" + (
                version ==~ /^\d+\.\d+\.\d+$/ ?
                    "${System.properties['maven.repository.releases']}" :
                    "${System.properties['maven.repository.snapshots']}"
            )
            allowInsecureProtocol = true
            credentials {
                username = "${System.properties['maven.repository.username']}"
                password = "${System.properties['maven.repository.password']}"
            }
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    // Jackson Json
    api 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    // Logging
    implementation 'ch.qos.logback:logback-classic:1.4.4'
    implementation 'org.slf4j:log4j-over-slf4j:2.0.3'

    // JUnit
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    // Spock
    testImplementation 'org.codehaus.groovy:groovy:3.0.13'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation 'org.objenesis:objenesis:3.2'
}

test {
    useJUnitPlatform()
}

task allWrappers(type: Wrapper) {
    gradleVersion = "${project.property('gradle.version')}"
}

changelog {
    path = "${project.projectDir}/CHANGELOG.md"
    header = "${-> version.get()} - ${new SimpleDateFormat("yyyy-MM-dd").format(new Date())}"
    headerParserRegex = ~/(\d+\.\d+\.\d+.+)/
    itemPrefix = "-"
    keepUnreleasedSection = true
    unreleasedTerm = "Unreleased work"
    groups = ["Features", "Fixed", "Security"]
}

release {
    git {
        requireBranch = 'main'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

dependencyCheck {
    format='ALL'
}

sonarqube {
    properties {
        property 'sonar.dependencyCheck.severity.blocker', '9.0'
        property 'sonar.dependencyCheck.severity.critical', '7.0'
        property 'sonar.dependencyCheck.severity.major', '4.0'
        property 'sonar.dependencyCheck.severity.minor', '0.0'

        property 'sonar.dependencyCheck.jsonReportPath', 'build/reports/dependency-check-report.json'
        property 'sonar.dependencyCheck.htmlReportPath', 'build/reports/dependency-check-report.html'
    }
}

tasks.jacocoTestReport.dependsOn tasks.test
tasks.sonarqube.dependsOn tasks.dependencyCheckAnalyze
tasks.sonarqube.dependsOn tasks.jacocoTestReport
